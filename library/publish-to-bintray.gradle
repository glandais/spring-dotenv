apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def pomConfig = {
    packaging 'jar'
    developers {
        developer {
            id 'paulschwarz'
            name 'Paul Schwarz'
            email 'paul@paulschwarz.me'
        }
    }
    scm {
        url 'https://github.com/paulschwarz/spring-dotenv'
        connection 'scm:git:git://github.com/paulschwarz/spring-dotenv.git'
        developerConnection 'scm:git:git://github.com/paulschwarz/spring-dotenv.git'
    }
    issueManagement {
        system 'GitHub'
        url 'https://github.com/paulschwarz/spring-dotenv/issues'
    }
    licenses {
        license {
            name 'The MIT License'
            url 'http://opensource.org/licenses/MIT'
            distribution 'repo'
        }
    }
}

publishing {
    publications {
        library(MavenPublication) {
            from components.java
            groupId 'me.paulschwarz'
            artifactId 'spring-dotenv'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Provides a Dotenv property source for Spring')
                root.appendNode('name', 'spring-dotenv')
                root.appendNode('url', 'https://github.com/paulschwarz/spring-dotenv')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = prop('bintray.user', 'BINTRAY_USER')
    key = prop('bintray.apikey', 'BINTRAY_API_KEY')
    publish = true
    publications = ['library']
    pkg {
        repo = 'maven'
        name = 'spring-dotenv'
        websiteUrl = 'https://github.com/paulschwarz/spring-dotenv'
        issueTrackerUrl = 'https://github.com/paulschwarz/spring-dotenv/issues'
        vcsUrl = 'https://github.com/paulschwarz/spring-dotenv.git'
        licenses = ['MIT']
        labels = ['spring', 'springboot', 'dotenv', '12factor', '12factor app', 'config', 'environment', 'env']
        publicDownloadNumbers = true
        githubRepo = 'paulschwarz/spring-dotenv'
        githubReleaseNotesFile = 'README.md'
        version {
            name = project.version
            vcsTag = "v${project.version}"
            released = new Date()
            gpg {
                sign = true
                passphrase = prop('signing.password', 'SIGNING_PASSWORD')
            }
        }
    }
}
